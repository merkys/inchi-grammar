(* Uncomment to run multi-string tests at *)
(* https://mdkrajnak.github.io/ebnftest/ *)
(* <test>              ::= <string> ( "\n" <string>)*; *)
<string>            ::= <prefix> <formula>? <connections>? <h_atoms>? ;
<formula>           ::= "/" <elemental> ( "." <elemental> )* ;
<connections>       ::= "/c" <graph> ( ";" <graph> )* ;
<h_atoms>           ::= "/h"
                        ( ( <vh_count> ( "," <mh_count> )* ) | <mh_count> ) ;
<elemental>         ::= <c>? <h>? <br>? <cl>? <f>? <i>? <n>? <o>? <p>? <s>? ;
<c>                 ::= "C" <count>? ;
<h>                 ::= "H" <count>?;
<br>                ::= "Br" <count>? ;
<cl>                ::= "Cl" <count>? ;
<f>                 ::= "F" <count>? ;
<i>                 ::= "I" <count>? ;
<n>                 ::= "N" <count>? ;
<o>                 ::= "O" <count>? ;
<p>                 ::= "P" <count>? ;
<s>                 ::= "S" <count>? ;
(* only 108 to go... *)
<graph>             ::= <index> <body> ;
<body>              ::= ( "-" | <branch>+ ) <tail> ;
<branch>            ::= "(" ( <index> ( ( "," <index> ) | <body> )* ) ")" ;
<tail>              ::= <index> <body>* ;
<vh_count>          ::= <virtual_hydrogens> ( "," <virtual_hydrogens> )* ;
<mh_count>          ::= <mobile_hydrogens>+ ;
<virtual_hydrogens> ::= <index> ( ( "," |  "-" ) <index> )* "H" <count>? ;
<mobile_hydrogens>  ::= "(H" ( "-" | <count> )? ( "," <index> )+ ")" ;
<index>             ::= <nonzero> <digit>* ;
<count>             ::= ( "1" <digit>+ ) | ( <twoplus> <digit>* ) ;
<digit>             ::= "0" | <nonzero> ;
<nonzero>           ::= "1" | <twoplus> ;
<prefix>            ::= "InChI=1S" ;
<twoplus>           ::= "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" ;
